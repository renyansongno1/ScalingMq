buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath('com.bmuschko:gradle-docker-plugin:7.4.0')
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.19"
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: "com.google.protobuf"

dependencies {
    implementation(
            'io.grpc:grpc-stub:1.49.0',
            'io.grpc:grpc-protobuf:1.49.0',
            'io.netty:netty-all:4.1.80.Final',
            // https://mvnrepository.com/artifact/com.intel.pmem/llpl
            'com.intel.pmem:llpl:1.2.1-release',
            'ch.qos.logback:logback-core:1.2.3',
            'ch.qos.logback:logback-classic:1.2.3',
            // https://mvnrepository.com/artifact/dnsjava/dnsjava
            'dnsjava:dnsjava:3.5.1',

            project(":scalingMq-storage-api"),
            project(":scalingMq-common")
    )
}

shadowJar {
    zip64 true
    manifest {
        attributes 'Main-Class': 'org.scalingmq.storage.ScalingMqStorageApplication'
    }
}

docker {
    javaApplication {
        baseImage = 'prasadlvi/openjdk-17-jre'
        ports = [9876, 4321]
        images = ['scalingmq-storage-component:v1.0', 'scalingmq-storage-component:latest']
        jvmArgs = ['-XX:InitialRAMPercentage=60', '-XX:MaxRAMPercentage=60']
    }
}

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.15.0:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.15.0'
        }

    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0:osx-x86_64'
            } else {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
            }

        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "$projectDir/gen"
}

clean {
    delete protobuf.generatedFilesBaseDir
}