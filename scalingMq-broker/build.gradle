apply plugin: "com.google.protobuf"
apply plugin: 'com.bmuschko.docker-java-application'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.19"
        classpath('com.bmuschko:gradle-docker-plugin:7.4.0')
    }
}

dependencies {
    implementation(
            'io.grpc:grpc-stub:1.40.2',
            'io.grpc:grpc-protobuf:1.40.2',
            'io.netty:netty-all:4.1.80.Final',
            'ch.qos.logback:logback-core:1.2.3',
            'ch.qos.logback:logback-classic:1.2.3',
            // https://mvnrepository.com/artifact/com.google.code.gson/gson
            'com.google.code.gson:gson:2.9.1',

            project(':scalingMq-common'),
            project(':scalingMq-route-client')
    )
}

protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.15.0:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.15.0'
        }

    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0:osx-x86_64'
            } else {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.4.0'
            }

        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "$projectDir/gen"
}

docker {
    javaApplication {
        baseImage = 'folioci/alpine-jre-openjdk17:2.0.2'
        ports = [7654,6543]
        images = ['scalingmq-broker-component:v1.0', 'scalingmq-broker-component:latest']
        jvmArgs = ['-XX:InitialRAMPercentage=60', '-XX:MaxRAMPercentage=60']
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}